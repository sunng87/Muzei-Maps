apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    def Properties keyProps = new Properties()
    keyProps.load(new FileInputStream(file('keys.properties')))
    def Properties signingProps = new Properties()
    signingProps.load(new FileInputStream(file('../local.properties')))

    def _versionName = "2.1.0"
    def _versionCode = 14

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 22
        versionCode _versionCode
        versionName _versionName

        resValue "string", "avoscloud_api_id", keyProps['avoscloud_api_id']
        resValue "string", "avoscloud_api_secret", keyProps['avoscloud_api_secret']
        resValue "string", "pref_title_about", ("Mapzei " + _versionName)
        resValue "string", "snazzy_api_key", keyProps['snazzy_api_key']
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    signingConfigs {
        config1 {
            keyAlias signingProps['key.alias']
            keyPassword signingProps['key.pass']
            storeFile file(signingProps['key.store'])
            storePassword signingProps['store.pass']
        }
    }
    buildTypes {
        debug {
            resValue "string","mapbox_map_id",keyProps['mapbox_map_id']
        }
        release {
            resValue "string","mapbox_map_id", ""
            minifyEnabled false
            signingConfig signingConfigs.config1
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile 'com.google.android.apps.muzei:muzei-api:2+'
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.squareup.okhttp:okhttp:2.1.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'
    compile 'cn.leancloud.android:avoscloud-sdk:v2.6.+'
}
