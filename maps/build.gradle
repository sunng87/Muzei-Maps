apply plugin: 'android'

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.2"

    def Properties keyProps = new Properties()
    keyProps.load(new FileInputStream(file('keys.properties')))
    def Properties signingProps = new Properties()
    signingProps.load(new FileInputStream(file('../local.properties')))

    def _versionName = "1.3.1"
    def _versionCode = 11

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 19
        versionCode _versionCode
        versionName _versionName

        resValue "string", "avoscloud_api_id", keyProps['avoscloud_api_id']
        resValue "string", "avoscloud_api_secret", keyProps['avoscloud_api_secret']
        resValue "string", "pref_title_about", ("Mapzei " + _versionName)
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    signingConfigs {
        config1 {
            keyAlias signingProps['key.alias']
            keyPassword signingProps['key.pass']
            storeFile file(signingProps['key.store'])
            storePassword signingProps['store.pass']
        }
    }
    buildTypes {
        debug {
            resValue "string","mapbox_map_id",keyProps['mapbox_map_id']
        }
        release {
            resValue "string","mapbox_map_id", ""
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.config1
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile 'com.google.android.apps.muzei:muzei-api:+'
    compile 'com.android.support:support-v4:+'
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
